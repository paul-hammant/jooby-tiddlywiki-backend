buildscript {
    ext {
        kotlinVersion = '1.2.21'
        joobyVersion = '1.3.0'
        mapdbVersion = '3.0.5'

        junitPlatformVersion = "1.0.0"
        spekVersion = "1.1.5"
    }

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jooby:jooby-gradle-plugin:$joobyVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
    }
}

group 'wiki.tiddly'
version '1.0-SNAPSHOT'

apply plugin: "io.spring.dependency-management"
apply plugin: "com.google.osdetector"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "jooby"
apply plugin: "org.junit.platform.gradle.plugin"

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.jooby:jooby-bom:$joobyVersion"
    }
}

mainClassName = "tiddly.AppKt"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jooby:jooby-lang-kotlin"
    compile "org.jooby:jooby-netty"
    compile "org.jooby:jooby-jackson"
    compile "org.jooby:jooby-rxjava"

    compile "org.mapdb:mapdb:$mapdbVersion"

    compile "io.netty:netty-tcnative-boringssl-static:${dependencyManagement.importedProperties['boringssl.version']}:${osdetector.classifier}"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile "org.jetbrains.spek:spek-api:$spekVersion"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"
    testCompile "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    testCompile "org.amshove.kluent:kluent:1.23"
    testCompile "io.rest-assured:rest-assured:3.0.5"
//    testCompile 'com.natpryce:hamkrest:1.4.2.2'

    testCompile "org.seleniumhq.selenium:selenium-java:3.11.0"
    testCompile "org.seleniumhq.selenium.fluent:fluent-selenium:1.19"
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile "io.github.bonigarcia:webdrivermanager:2.1.0"
}

/** We diverge from the default resources structure to adopt the Jooby standard: */
sourceSets.main.resources {
    srcDirs = ["conf", "public"]
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
